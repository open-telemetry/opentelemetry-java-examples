plugins {
    id "com.diffplug.spotless"
    id "com.github.johnrengelman.shadow" apply false
}

ext {
  // this line is managed by .github/scripts/update-sdk-version.sh
  otelSdkVersion = "1.19.0"
  // this line is managed by .github/scripts/update-instrumentation-version.sh
  otelInstrumentationVersion = "1.19.2"

  grpcVersion = '1.47.0'
  protobufVersion = '3.21.2'
  protocVersion = protobufVersion
}

println("Building against OpenTelemetry SDK version: $otelSdkVersion")
println("Building against OpenTelemetry Instrumentation version: $otelInstrumentationVersion")

subprojects {
    apply plugin: 'eclipse'
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'com.github.johnrengelman.shadow'

    group = "io.opentelemetry"
    version = "0.1.0-SNAPSHOT"

    repositories {
        mavenCentral()
        maven {
            // Add snapshot repository
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    compileJava {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }

    dependencies {
        implementation platform("io.opentelemetry:opentelemetry-bom:${otelSdkVersion}")
        implementation platform("io.opentelemetry:opentelemetry-bom-alpha:${otelSdkVersion}-alpha")
        implementation platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom:${otelInstrumentationVersion}")
        implementation platform("io.opentelemetry.instrumentation:opentelemetry-instrumentation-bom-alpha:${otelInstrumentationVersion}-alpha")
        implementation platform("io.grpc:grpc-bom:${grpcVersion}")
    }

    spotless {
        java {
            targetExclude '**/generated/**'
            googleJavaFormat()
        }
    }
}
