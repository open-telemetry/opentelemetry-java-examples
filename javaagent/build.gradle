plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.13'
    id 'io.spring.dependency-management' version '1.1.2'
    id 'de.undercouch.download' version '5.4.0'
}

description = 'OpenTelemetry Example for Java Agent'
ext.moduleName = "io.opentelemetry.examples.javagent"

bootRun {
    mainClass.set 'io.opentelemetry.example.javagent.Application'
}

bootJar {
    dependsOn("downloadAgent")
}

dependencies {
    implementation("io.opentelemetry:opentelemetry-api")

    //spring modules
    implementation("org.springframework.boot:spring-boot-starter-web")
}

// Download the OpenTelemetry java agent and put it in the build directory
task downloadAgent(type: Download) {
    src "https://github.com/open-telemetry/opentelemetry-java-instrumentation/releases/latest/download/opentelemetry-javaagent.jar"
    dest project.buildDir.toString() + "/otel/opentelemetry-javaagent.jar"
    overwrite true
}
