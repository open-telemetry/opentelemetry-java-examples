plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.13'
    id 'io.spring.dependency-management' version '1.1.2'
}

description = 'OpenTelemetry Example for Java Agent'
ext.moduleName = "io.opentelemetry.examples.javagent"

configurations {
    // create a separate configuration for the agent since it should not be a normal dependency
    agent
}

dependencies {
    implementation("io.opentelemetry:opentelemetry-api")

    //spring modules
    implementation("org.springframework.boot:spring-boot-starter-web")

    agent("io.opentelemetry.javaagent:opentelemetry-javaagent:1.28.0")
}

def copyAgent = tasks.register('copyAgent', Copy) {
    from configurations.agent.singleFile
    into layout.buildDirectory.dir('agent')
    rename 'opentelemetry-javaagent-.*\\.jar', 'opentelemetry-javaagent.jar'
}

bootJar {
    dependsOn copyAgent

    archiveFileName = 'app.jar'
}
